describe 'fsFormFor', ->
  require '../../src/coffee/formFor'
  $scope = null
  $compile = null

  beforeEach angular.mock.module('formstamp')

  beforeEach inject ($rootScope, _$compile_) ->
    $scope = $rootScope.$new()

    $scope.submitCount = 0

    $scope.submit = (event) ->
      $scope.submitCount = $scope.submitCount + 1
      event.preventDefault()
      return false

    $scope.titles = ['developer', 'manager', 'ceo', 'qa']

    $scope.user =
      name: "John Doe"
      email: "johndoe@example.com"
      title: "developer"

    $compile = _$compile_

  compile = (elem) ->
    elem ||= """
<form fs-form-for model="user" ng-submit="submit($event)">
  <fs-input as="text" name="name" required="" label="Name"></fs-input>
  <fs-input as="email" name="email" required="" label="Email"></fs-input>
  <fs-input as="fs-select" name="title" required="" label="Title" items="titles" freetext=""></fs-input>
</form fs-form-for>
    """
    element = $compile(elem)($scope)
    $scope.$apply()
    element

  input = {}
  errors = {}
  enterAndClearDateValueTest = () ->
    input.val('11:30').triggerHandler('input')
    expect(errors.find('li').length).toBe 0
    expect(errors.hasClass('ng-hide')).toBeTruthy

    input.val('').triggerHandler('input')
    expect(errors.find('li').length).toBe 1
    expect(errors.hasClass('ng-hide')).toBeFalsy

  describe "validations", ->

    describe "when there is only one unnamed form", ->
      elem = element = ""
      beforeEach () ->
        elem = """
<div>
  <form fs-form-for model="user">
    <fs-input as="fs-time" name="time" required label="Time"></fs-input>
  </form fs-form-for>
</div>
        """
        element = compile(elem)

      it 'should auto name it "autoGeneratedFormName1"', ->
        expect(element.find('form')
                      .filter(() -> @name.match(/autoGeneratedFormName\d/))
                      .length).toBe 1

    describe "when there is only one named form", ->
      elem = element = ""
      beforeEach () ->
        elem = """
<div>
  <form fs-form-for model="user" name="userForm">
    <fs-input as="fs-time" name="time" required label="Time"></fs-input>
  </form fs-form-for>
</div>
        """
        element = compile(elem)

      it 'should keep original form name', ->
        expect(element.find('form[name=userForm]').length).toBe 1

      it 'should not find autonamed form with name=autoGeneratedFormName1', ->
        expect(element.find('form')
                      .filter(() -> @name.match(/autoGeneratedFormName\d/))
                      .length).toBe 0

    describe "when there are multiple unnamed forms", ->
      elem = element = ""
      beforeEach () ->
        elem = """
<div>
  <form fs-form-for model="user1">
    <fs-input as="fs-time" name="time1" required label="Time1"></fs-input>
    <fs-input as="fs-time" name="time2" required label="Time2"></fs-input>
  </form fs-form-for>
  <form fs-form-for model="user2">
    <fs-input as="fs-time" name="time3" required label="Time3"></fs-input>
  </form fs-form-for>
</div>
        """
        element = compile(elem)

      it 'should auto name it "autoGeneratedFormName1" and "autoGeneratedFormName2" ', ->
        forms = element.find('form')
                       .filter(() -> @name.match(/autoGeneratedFormName\d/))
        firstFormIndex = parseInt(/autoGeneratedFormName(\d)/.exec(forms[0].name)[1])
        secondFormIndex = parseInt(/autoGeneratedFormName(\d)/.exec(forms[1].name)[1])
        expect(secondFormIndex).toBe firstFormIndex+1

      it 'should show validation error for first input of first form', ->
        input = element.find('form').find('input').eq(0)
        errors = element.find('form').find('.fs-errors').eq(0)
        enterAndClearDateValueTest()

      it 'should show validation error for first input of second form', ->
        input = element.find('form').eq(1).find('input').eq(0)
        errors = element.find('form').eq(1).find('.fs-errors').eq(0)
        enterAndClearDateValueTest()

    describe "when there are multiple named forms", ->
      elem = element = ""
      beforeEach () ->
        elem = """
<div>
  <form fs-form-for model="user1" name="firstaForma">
    <fs-input as="fs-time" name="time1" required label="Time1"></fs-input>
    <fs-input as="fs-time" name="time2" required label="Time2"></fs-input>
  </form fs-form-for>
  <form fs-form-for model="user2" name="secondaForma">
    <fs-input as="fs-time" name="time3" required label="Time3"></fs-input>
  </form fs-form-for>
</div>
        """
        element = compile(elem)

      it 'should show validation error for first input of first form', ->
        input = element.find('form').find('input').eq(0)
        errors = element.find('form').find('.fs-errors').eq(0)
        enterAndClearDateValueTest()

      it 'should show validation error for first input of second form', ->
        input = element.find('form').eq(1).find('input').eq(0)
        errors = element.find('form').eq(1).find('.fs-errors').eq(0)
        enterAndClearDateValueTest()




  it 'should expands', ->
    element = compile()
    expect(element.find("fs-input").length).toBe(0)
    expect(element.find("input[type='text']").length).toBe(2)
    expect(element.find("input[type='email']").length).toBe(1)

  it 'should wrap each control with .form-group', ->
    element = compile()
    expect(element.find('.form-group').length).toBe 3

  it 'should create label for each control', ->
    element = compile()
    expect(element.find('.form-group > label.control-label').length).toBe 3

  it 'should bind widgets to attributes of model', ->
    element = compile()
    expect(element.find('input[ng-model="user.name"]').length).toBe 1
    expect(element.find('input[ng-model="user.email"]').length).toBe 1
    expect(element.find('div[ng-model="user.title"]').length).toBe 1

  it 'should register only 3 inputs in FormController', ->
    element = compile()
    formCtrl = element.data('$formController')
    attrs = (name for name, val of formCtrl when '$' not in name)
    expect(attrs).toEqual(['name', 'email', 'title'])

  it 'should perform submit only once', ->
    element = compile()
    element.submit()
    expect($scope.submitCount).toBe 1
